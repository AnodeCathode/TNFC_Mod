buildscript {
    repositories {
        jcenter()
        //maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }

    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

// Load properties file, easier to manage versions of required dependencies
file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

if (System.getenv('VERSION') != null) {
    version = System.getenv('VERSION')
} else {
    version = "12.2.81-rc3"
}
group = "tnfcmod"
archivesBaseName = "TNFCMod"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
    runDir = "run"
    mappings = "stable_39"

    def args = [
            "-Dmixin.debug.export=true",
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true'
    ]

    replace("@VERSION@", project.version)
    replaceIn("tnfcmod.java")
}

repositories {
    // Repositories required for dependencies, not ForgeGradle go here.
    mavenCentral()
    maven {
        name = "CraftTweaker Maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
//    maven {
//        name = "Hwyla maven"
//        url "http://tehnut.info/maven"
//    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven {
        name = "CoFH Maven"
        url = "https://maven.covers1624.net/"
    }
        maven {
        name = "ChickenBones Maven"
        url = "http://chickenbones.net/maven/"
    }

    maven { // ModMaven, there is a bunch of stuff on there
        name 'ModMaven'
        url "http://modmaven.k-4u.nl/"
    }
    maven { // Shadowfacts - Forgelin
        name 'Shadowfacts'
        url "http://maven.shadowfacts.net/"
    }
    maven {
        name = "Modmuss50"
        url = "http://maven.modmuss50.me/"
    }
    maven { //Guidebook ffs
        url 'http://dogforce-games.com/maven'
    }
    maven {
        url "https://cursemaven.com"
    }
    maven {
        name = "CleanroomMC"
        url "https://maven.cleanroommc.com"
    }
}

String mcVersion = config.minecraft.version
String shortVersion = mcVersion.substring(0, mcVersion.lastIndexOf('.'))
String strippedVersion = shortVersion.replace(".", "") + "0"

mixin {
    add sourceSets.main, "mixins.tnfcmod.refmap.json"
}

dependencies {
    deobfProvided ("CraftTweaker2:CraftTweaker2-MC${strippedVersion}-Main:${config.crafttweaker.version}")
    
    // deobfCompile ("mezz.jei:jei_${config.minecraft.version}:${config.jei.version}")
    // deobfCompile ("cofh:RedstoneFlux:1.12-2.0.1+:deobf")
    deobfCompile("curse.maven:cofhcore-69162:2920433")
    deobfCompile("betterwithmods:BetterWithMods:1.12-2.1.+") {
        exclude group: 'mezz.jei', module:'jei_1.12.2'
        exclude group: 'cofh', module:'RedstoneFlux'
    }
    deobfCompile "zone.rong:mixinbooter:4.2"

    compile fileTree(include: ["*.jar"], dir: "libs")
    compileOnly fileTree(include: ["*.jar"], dir: "libsCompile")
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", config.version
    inputs.property "mc_version", config.minecraft.version

    // copy mcmod.info from resources
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion variables/placeholders
        expand "mod_version": version, "mc_version": config.minecraft.version
    }

    // copy everything else from resources
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    // from the project root, include the LICENSE file
    from('.') {
        include "LICENSE.txt"
    }
}
jar {
    manifest {
        attributes (['FMLAT' : 'tnfc_at.cfg',
                ])
    }
}
// Deobf jar allows people to drop this in mod folder on a dev environment
task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    classifier "deobf"
}

project.tasks.withType(Jar) { jarTask -> // For all jar tasks
    jarTask.appendix = "MC" + project.minecraft.version
}
